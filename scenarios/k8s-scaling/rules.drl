import io.github.hephaestusmetrics.model.metrics.Metric;
import org.k8loud.executor.actions.kubernetes.HorizontalScalingAction
import org.k8loud.executor.actions.moam.statemanager.UpdateEntityAction

global org.k8loud.executor.drools.UsableServices usableServices;
global org.k8loud.executor.drools.CronChecker cronChecker;

dialect "mvel"

rule "scale-front-end-up"
    when
        latencyMetric : Metric(
            queryTag == "front-end-latency",
            latency: value
        )
        replicasAvailableMetric : Metric(
            queryTag == "front-end-replicas-available",
            replicasAvailable: value
        )
        replicasUnavailableMetric : Metric(
            queryTag == "front-end-replicas-unavailable",
            replicasUnavailable: value
        )
        eval(latency > 60 &&
             cronChecker.checkPatternForSession("0 0/2 * ? * * *"))
    then
        System.out.println("Processing 'scale-front-end-up'...");

        latencyTarget = 60.0;
        efficiency = 0.8;
        latencyErrorRatio = (latency - latencyTarget) / latencyTarget;

        newReplicas = (int) Math.max(1, Math.ceil(replicasAvailable * (1 + latencyErrorRatio) / efficiency));
        deltaReplicas = (int) Math.max(0, newReplicas - replicasAvailable - replicasUnavailable);

        stateManagerService = usableServices.getStateManagerService();
        UpdateEntityAction.builder()
            .query("select * from entity where api_version = 'kubernetes.crossplane.io/v1alpha2' and kind = 'Object' and name = '09-front-end-dep'")
            .lambdas("{\".spec.forProvider.manifest.spec.replicas\": \". + " + deltaReplicas + "\"}")
            .stateManagerService(stateManagerService)
            .build()
            .execute();

        System.out.println("...Processed 'scale-front-end-up'");
end

rule "scale-front-end-down"
    when
        latencyMetric : Metric(
            queryTag == "front-end-latency",
            latency: value
        )
        replicasAvailableMetric : Metric(
            queryTag == "front-end-replicas-available",
            replicasAvailable: value
        )
        replicasUnavailableMetric : Metric(
            queryTag == "front-end-replicas-unavailable",
            replicasUnavailable: value
        )
        eval(latency < 20 &&
             cronChecker.checkPatternForSession("0 0/2 * ? * * *"))
    then
        System.out.println("Processing 'scale-front-end-down'...");

        latencyTarget = 20.0;
        efficiency = 0.8;
        latencyErrorRatio = (latency - latencyTarget) / latencyTarget;

        newReplicas = (int) Math.max(1, Math.floor(replicasAvailable * (1 + latencyErrorRatio) / efficiency));
        deltaReplicas = (int) Math.min(0, newReplicas - replicasAvailable - replicasUnavailable);

        stateManagerService = usableServices.getStateManagerService();
        UpdateEntityAction.builder()
            .query("select * from entity where api_version = 'kubernetes.crossplane.io/v1alpha2' and kind = 'Object' and name = '09-front-end-dep'")
            .lambdas("{\".spec.forProvider.manifest.spec.replicas\": \". + " + deltaReplicas + "\"}")
            .stateManagerService(stateManagerService)
            .build()
            .execute();

        System.out.println("...Processed 'scale-front-end-down'");
end
